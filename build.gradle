plugins {
	id 'java'
	id 'war'
	id 'pmd'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'de.esempe'
version = '0.1'

tasks.named('war') {
    archiveFileName.set('workflow.war') // Set the desired WAR file name
} 

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

pmd {
    consoleOutput = false
    toolVersion = "6.39.0"
    rulesMinimumPriority = 3
    ruleSetFiles = files("./config/pmd/pmdruleset.xml")
    ruleSets = []
    ignoreFailures = true
}

repositories {
	mavenCentral()
}

dependencies {

	// Web-Services
	implementation 'org.springframework.boot:spring-boot-starter-web'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	
	// Datenbanken
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	runtimeOnly 'org.postgresql:postgresql'
	// Validierung
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	
	// Groovy als Skript einbinden
	implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.23'
	implementation group: 'org.apache.groovy', name: 'groovy-json', version: '4.0.23'
	implementation group: 'org.apache.groovy', name: 'groovy-jsr223', version: '4.0.23'
	
		
	// für OpenAPI: Web-Root "demo" -->
	// JSON: http://localhost:8080/demo/v3/api-docs
	// YAML: http://localhost:8080/demov3/api-docs.yaml
	// Swagger UI
	//http://localhost:8080/demo/swagger-ui/index.html
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.6.0'
	
	
	// ****************************************************************************
	// Nicht-Spring
	
	// Guava
	compileOnly 'com.google.guava:guava:31.1-jre'
	// Json Web Token (JWT)
	implementation 'com.auth0:java-jwt:3.18.3'
	
	// ****************************************************************************
	// Alles fürs Testen
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation "org.junit.platform:junit-platform-suite:1.9.2"

	
	// implementation 'org.springframework.session:spring-session-core'
}

test {
	useJUnitPlatform {
        includeTags("unit-test")
        includeEngines("junit-jupiter")
    }
}

// Tasks für Docker
String currentDir = './src/main/docker'

// Deploy WAR in (Auto-)Deploy-Verzeichnis vom Tomcat
task (deployWar, type: Exec, group: 'deploy') {
	dependsOn(war)
	workingDir currentDir
    commandLine 'sh','removewar.sh'
}


task (upDC, type: Exec, group: 'docker') {
    workingDir currentDir
    commandLine 'docker-compose','up', '-d'
}

task (downDC, type: Exec, group: 'docker') {
    workingDir currentDir
    commandLine 'docker-compose','down', '-t', '0'
}

